!!ARBvp1.0
#
# Vertex program for rendering GLMesh instances in 3D, colouring them
# according to vertex data.
#
# Input parameters:
#
#    {{ param3_normmat }} - Matrix to transform normal vectors into
#                           display coordinates.
#    {{ param4_mvmat }}   - Model view matrix.
#    {{ param4_mvpmat }}  - Model view projection matrix.
#
# Input attributes:
#    {{ attr_vertexData }} - Data associated with the current vertex
#    {{ attr_normal }}     - Vertex normal vector
#
# Outputs:
#    {{ varying_vertexData }}   - As above, passed through to fragment shader.
#    {{ varying_modulateData }} - As above, passed through to fragment shader.
#    {{ varying_vertex }}       - The vertex position in view coordinates.
#    {{ varying_normal }}       - As above, passed through to fragment shader.
#
# Author: Paul McCarthy <pauldmccarthy@gmail.com>
#

PARAM normmat[3] = {{ param3_normmat }};
PARAM mvmat[4]   = {{ param4_mvmat }};
PARAM mvpmat[4]  = {{ param4_mvpmat }};

TEMP normal;

DP4 result.position.x, mvpmat[0], vertex.position;
DP4 result.position.y, mvpmat[1], vertex.position;
DP4 result.position.z, mvpmat[2], vertex.position;
DP4 result.position.w, mvpmat[3], vertex.position;

# Transform the vertex position into display coordinates
DP4 {{ varying_vertex }}.x, mvmat[0], vertex.position;
DP4 {{ varying_vertex }}.y, mvmat[1], vertex.position;
DP4 {{ varying_vertex }}.z, mvmat[2], vertex.position;
DP4 {{ varying_vertex }}.w, mvmat[3], vertex.position;

# Transform the normal vector
DP3 normal.x, normmat[0], {{ attr_normal }};
DP3 normal.y, normmat[1], {{ attr_normal }};
DP3 normal.z, normmat[2], {{ attr_normal }};

# Normalise to unit length
DP3 normal.w, normal, normal;
RSQ normal.w, normal.w;
MUL normal,   normal, normal.w;
MOV normal.w, 0;

MOV {{ varying_normal }}, normal;

# Copy the vertex/modulate data
MOV {{ varying_vertexData }},   {{ attr_vertexData }};
MOV {{ varying_modulateData }}, {{ attr_modulateData }};

END
